////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПолучитьКонстанту(Ответ, Константа, ДатаКонстанты, ВозвращатьСписок, ТипКлюча) Экспорт

	Запрос = Новый Запрос;

	Запрос.Текст = "ВЫБРАТЬ
				   |	composit_ЛокальныеКонстанты.Ссылка КАК Константа,
				   |	composit_ЛокальныеКонстанты.ЭтоПароль КАК ЭтоПароль,
				   |	composit_ЛокальныеКонстанты.Логин
				   |ПОМЕСТИТЬ ЛокальныеКонстанты
				   |ИЗ
				   |	ПланВидовХарактеристик.composit_ЛокальныеКонстанты КАК composit_ЛокальныеКонстанты
				   |ГДЕ
				   |	composit_ЛокальныеКонстанты.Ссылка В (&МассивКонстант)
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	КР.Константа КАК Константа,
				   |	КР.Значение КАК Значение,
				   |	КР.ХранилищеКоллекций КАК Коллекция
				   |ПОМЕСТИТЬ ЗначенияКонстант
				   |ИЗ
				   |	РегистрСведений.composit_ЛокальныеКонстантыЗначения.СрезПоследних(&ДатаКонстанты, Константа В (&МассивКонстант)) КАК
				   |		КР
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	ЛокальныеКонстанты.Константа КАК Константа,
				   |	ЛокальныеКонстанты.ЭтоПароль КАК ЭтоПароль,
				   |	ЗначенияКонстант.Значение КАК Значение,
				   |	ВЫБОР
				   |		КОГДА &ВернутьКолекцию
				   |			ТОГДА ЗначенияКонстант.Коллекция
				   |		ИНАЧЕ NULL
				   |	КОНЕЦ КАК Коллекция,
				   |	ВЫБОР
				   |		КОГДА &ТипКлюча = 0
				   |			ТОГДА ЛокальныеКонстанты.Константа.Код
				   |		ИНАЧЕ ВЫБОР
				   |			КОГДА &ТипКлюча = 1
				   |				ТОГДА ЛокальныеКонстанты.Константа.Идентификатор
				   |			ИНАЧЕ ЛокальныеКонстанты.Константа
				   |		КОНЕЦ
				   |	КОНЕЦ КАК КлючОтвета,
				   |	ЛокальныеКонстанты.Константа.Идентификатор КАК Идентификатор,
				   |	ЛокальныеКонстанты.Логин КАК Логин
				   |ИЗ
				   |	ЛокальныеКонстанты КАК ЛокальныеКонстанты
				   |		ЛЕВОЕ СОЕДИНЕНИЕ ЗначенияКонстант КАК ЗначенияКонстант
				   |		ПО (ЛокальныеКонстанты.Константа = ЗначенияКонстант.Константа)
				   |ИТОГИ
				   |ПО
				   |	Константа";

	Запрос.УстановитьПараметр("МассивКонстант", Константа);
	Запрос.УстановитьПараметр("ДатаКонстанты", ДатаКонстанты);
	Запрос.УстановитьПараметр("ВернутьКолекцию", ВозвращатьСписок);
	Запрос.УстановитьПараметр("ТипКлюча", ТипКлюча);

	ВыборкаГруппировка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаГруппировка.Следующий() Цикл
		Выборка = ВыборкаГруппировка.Выбрать();
		Если Выборка.Следующий() Тогда
			Если выборка.ЭтоПароль Тогда
				Пароль = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Выборка.идентификатор, "Пароль");

				Значение = Новый Структура;
				Значение.Вставить("Логин", Выборка.Логин);
				Значение.Вставить("Пароль", Пароль);
			Иначе
				Если ВозвращатьСписок Тогда

					Значение = Новый Структура;
					Значение.Вставить("Значение", Выборка.Значение);
					Значение.Вставить("Коллекция", Выборка.Коллекция);

				Иначе
					Значение = Выборка.Значение;
				КонецЕсли;
			КонецЕсли;

			Ответ.Вставить(Выборка.КлючОтвета, Значение);

		КонецЕсли;
	КонецЦикла;
КонецПроцедуры //ПолучитьКонстантуРазработки()

#КонецОбласти